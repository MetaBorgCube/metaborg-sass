module Expression

imports
  Common
  Colour

context-free start-symbols
  Exp
  Colour
  
context-free syntax

  // Literals
  Exp           = Number
  Exp.String    = String
  Exp           = Colour {prefer}
  Exp.Length    = <<Exp> <LengthUnit>> {prefer}
  Exp.True      = <true>
  Exp.False     = <false>
  Exp.Null      = <null>
  Exp.Var       = <$<ID>>

  Exp = <(<Exp>)> {bracket}

  // Arithmetic Operators
  Exp.Add   = <<Exp> + <Exp>> {left}
  Exp.Sub   = <<Exp> - <Exp>> {left}
  Exp.Mult  = <<Exp> * <Exp>> {left}
  Exp.Div   = <<Exp> / <Exp>> {left}
  Exp.Mod   = <<Exp> % <Exp>> {left}
  
  // Logical Operators
  Exp.And = <<Exp> and <Exp>> {left}
  Exp.Or  = <<Exp> or <Exp>> {left}
  Exp.Not = <not <Exp>> {right}
  
  // Comparison
  Exp.Eq    = <<Exp> == <Exp>> {non-assoc}
  Exp.InEq  = <<Exp> != <Exp>> {non-assoc}
  Exp.Gt    = [[Exp] > [Exp]]  {non-assoc}
  Exp.Gte   = [[Exp] >= [Exp]] {non-assoc}
  Exp.Lt    = [[Exp] < [Exp]]  {non-assoc}
  Exp.Lte   = [[Exp] <= [Exp]] {non-assoc}
  
  // String
  String.Quoted = STRING
  
  Number.Int    = INT
  Number.Float  = FLOAT
  
  Colour.HexShort = <#<HEXShorthand>>
  Colour.Hex      = <#<HEX>>
  Colour.ColourLabel = COLOUR-LITERAL

context-free restrictions
  Colour -/- [a-zA-Z0-9]

context-free priorities
  {non-assoc:
    Exp.Length
  } >
  {left:
      Exp.Not
  } >
  {left:
    Exp.Mult
    Exp.Div
    Exp.Mod
  } >
  {left:
      Exp.Add
      Exp.Sub
  } > {non-assoc:
      Exp.Gt
      Exp.Gte
      Exp.Lt
      Exp.Lte
  } >
  {left:
      Exp.And
  } > 
  {left:
    Exp.Or
  } >
  {non-assoc: 
      Exp.Eq
      Exp.InEq
  }


