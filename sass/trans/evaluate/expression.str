module evaluate/expression

imports
  nabl2/api
  nabl2shared

imports
  evaluate/evaluate
  evaluate/number
  src-gen/signatures/Expression-sig
  desugar/-
  analysis/sass

strategies
  extract: (l, r) -> (<extr> l, <extr> r)
  extract: Length(n, _) -> <extr> n
  extract: Int(f) -> <string-to-int> f
  extract: Float(f) -> <string-to-real> f
  extract: String(Quoted(s)) -> s
  extr-f: (l, r) -> (<extr-f> l, <extr-f> r)
  extr-f: f@Float(_) -> <extr> f
  extr-f: Int(f) -> <string-to-real> f
  
  extr = try(extract)
  evaluate-exp = try(evaluate-e)

  apply-bin(|f) = extr; call(f||); try(intl-float)
  
  wrap-result: (IntT(),   v) -> Int(v)
  wrap-result: (FloatT(), v) -> Float(v)
  wrap-result: (LengthT(eT, u), v) -> Length(<wrap-result> (eT, v), u)
  wrap-result: (StringT(), v) -> String(v)
  
  op-to-f: (Add(), IntT()) -> "addi"
  op-to-f: (Sub(), IntT()) -> "subti"
  op-to-f: (Mul(), IntT()) -> "muli"
  op-to-f: (Mod(), IntT()) -> "modi"
  
  op-to-f: (Add(), FloatT()) -> "addr"
  op-to-f: (Sub(), FloatT()) -> "subtr"
  op-to-f: (Mul(), FloatT()) -> "mulr"
  op-to-f: (Div(), FloatT()) -> "divr"
  op-to-f: (Mod(), FloatT()) -> "modr"
  
  op-to-f: (Add(), StringT()) -> "conc-strings"

  extr-math-type: LengthT(e, _) -> e  
  extr-comp-v: Length(e, _) -> e
/**
 * Variable lookup
 */  
rules
  evaluate-e: VarRef(x) -> e with
    a := <nabl2-get-ast-analysis>
    ; ref-occ := <nabl2-mk-occurrence(|"Var")> x
    ; dec-occ := <nabl2-get-resolved-name(|a); Fst> ref-occ
    ; vExp    := <nabl2-get-property(|a, "e")> dec-occ
    ; e       := <evaluate> vExp

/** 
 * Logic
 */
rules
  evaluate-e: BinExp(And(), lhs, rhs) -> 
    <?(True(), True()) < !True() + !False()> (lhs, rhs)
  evaluate-e: BinExp(Or(), lhs, rhs) -> 
    <?(True(), _) < !True() + (?(_, True()) < !True() + !False())> (lhs, rhs)
  evaluate-e: UnExp(Not(), e) -> <?True() < !False() + !True()> e


/**
 * Comparison
 */
rules
  evaluate-e: BinExp(Eq(), lhs, rhs) -> 
    <?(e, e) < !True() + !False()> (<strip-annos> lhs, <strip-annos> rhs)
  evaluate-e: BinExp(Gt(), lhs, rhs) ->  
    <extr-f; gtr < !True() + !False()> (lhs, rhs) 

  
/**
 * Math
 */
rules
  evaluate-e: e@BinExp(op, lhs, rhs) -> r with 
    a := <nabl2-get-ast-analysis>
    ; endT  := <nabl2-get-ast-type(|a)> e
    
    ; eT  := <try(extr-math-type)> endT
    ; eLV := <try(extr-comp-v)> lhs
    ; eRV := <try(extr-comp-v)> rhs
    
    ; f   := <op-to-f> (op, eT)
    ; r   := <wrap-result> (endT, v)
    ; v   := <apply-bin(|f)> (eLV, eRV)
