module analysis/declaration

imports
  analysis/sass

/**
 * Mixins
 */
rules
  RootScopeIterative[[ [MixinDeclaration(n, args, argsDefault, body) | succ] ^ (avS) ]] :=
    Mixin{n} <- avS
    
    , new argS
    , argS ---> avS
    , Mixin{n} =A=> argS
    
    , Map1(DeclareFArg)[[ args ^ (argS) ]]
    , Map1(DeclareFArg)[[ argsDefault ^ (argS) ]]
    , distinct/name D(argS)/Var | error $[Duplicate argument definition [NAME]] @NAMES
    
    , new bodyS
    , bodyS ---> argS
    
    , new finalMixinGS
    , ScopeIterative[[ body ^ (bodyS, avS, finalMixinGS) ]]
    
    , RootScopeIterative[[ succ ^ (finalMixinGS) ]]
    .

  ScopeIterative[[ [MixinDeclaration(n, args, defaults, body) | succ] ^ (avS, gS, finalGS) ]] :=
    Mixin{n} <- avS
    
    , new argS
    , argS ---> avS
    , Mixin{n} =A=> argS
    
    , Map1(DeclareFArg)[[ args ^ (argS) ]]
    , Map1(DeclareFArg)[[ defaults ^ (argS) ]]
    , distinct/name D(argS)/Var | error $[Duplicate argument definition [NAME]] @NAMES
    
    , new bodyS
    , bodyS ---> argS
    
    , new finalMixinGS
    , ScopeIterative[[ body ^ (bodyS, gS, finalMixinGS) ]]
    
    , new nextS
    , nextS -G-> finalMixinGS
    , nextS ---> avS
    , ScopeIterative[[ succ ^ (nextS, finalMixinGS, finalGS) ]]
    .
  
  DeclareFArg[[ FArg(i, FArg(n)) ^ (argS) ]] :=
    Var{n} <- argS
    , Var{n}: VarT()
    , Arg{n} <- argS
    , IArg{i} <- argS
    .
  DeclareFArg[[ FArg(i, FArgDefault(n, exp)) ^ (argS) ]] :=
    Var{n} <- argS
    , [[ exp ^ (s, _) : t ]]
    , Var{n} : t

    , ArgDefault{n} <- argS
    , IArgDefault{i} <- argS
    .


/**
 * Block declarations
 */
rules

  RootScopeIterative[[ [BlockDeclaration(selec, decs) | succ] ^ (avS) ]] :=
    new blockS, 
    blockS ---> avS,
    new finalBlockGS,
    ScopeIterative[[ decs ^ (blockS, avS, finalBlockGS) ]],
    RootScopeIterative[[ succ ^ (finalBlockGS) ]]. 
  ScopeIterative[[ [BlockDeclaration(selec, decs) | succ] ^ (avS, gS, finalGS) ]] :=
    new blockS,
    blockS ---> avS,
    new finalBlockGS,
    ScopeIterative[[ decs ^ (blockS, gS, finalBlockGS) ]],
    
    new nextS,
    nextS ---> avS,
    nextS -G-> finalBlockGS,
    ScopeIterative[[ succ ^ (nextS, finalBlockGS, finalGS) ]].


/**
 * Property declarations
 */
rules
  ScopeIterative[[ [PropertyDeclaration(prop, e) | succ] ^ (avS, gS, fGS) ]] :=
    [[ e ^ (avS, cssAbleCheck) : eT ]],
    eT <cssable? cssAbleCheck | error $[[eT] is not a valid CSS expression type.] @e,
    ScopeIterative[[ succ ^ (avS, gS, fGS) ]].


/**
 * Variable declarations
 */
rules
  RootScopeIterative[[ [vd@VarDeclaration(var, e) | succ] ^ (avS) ]] :=
    new nextS, 
    nextS -G-> avS,
    
    DeclareVar[[ vd ^ (avS, nextS) ]],
    RootScopeIterative[[ succ ^ (nextS) ]].
  ScopeIterative[[ [vd@VarDeclaration(var, e) | succ] ^ (avS, gS, fGS) ]] :=
    new nextS, 
    nextS ---> avS,
    
    DeclareVar[[ vd ^ (avS, nextS) ]],
    ScopeIterative[[ succ ^ (nextS, gS, fGS) ]].


  RootScopeIterative[[ [gvd@GlobalVarDeclaration(var, e) | succ] ^ (avS) ]] :=
    new s,
    s -G-> avS,
    
    DeclareVar[[ gvd ^ (avS, s) ]],
    RootScopeIterative[[ succ ^ (s) ]].
  ScopeIterative[[ [gvd@GlobalVarDeclaration(var, e) | succ] ^ (avS, gS, fGS) ]] :=
    new globalS,  globalS -G-> gS,
    new s,        s ---> avS,
    s -G-> globalS,
    
    DeclareVar[[ gvd ^ (avS, globalS) ]],
    ScopeIterative[[ succ ^ (s, globalS, fGS) ]].


  DeclareVar[[ VarDeclaration(var, e) ^ (avS, decS) ]] :=
    [[ e ^ (avS, _) : t ]],
    Var{var} <- decS,
    Var{var} : t,
    Var{var}.e := e.
  DeclareVar[[ GlobalVarDeclaration(var, e) ^ (avS, gS) ]] :=
    [[ e ^ (avS, _) : t ]],
    Var{var} <- gS,
    Var{var} : t,
    Var{var}.e := e.
