module analysis/literals

imports
  src-gen/signatures/Expression-sig
  desugar/expression/-
  analysis/sass
  
  nabl2/api
  
rules
  /*
   * Literals
   */
  [[ String(_, _) ^ (_)   : StringT() ]].
  [[ Int(_) ^ (_)         : IntT()    ]].
  [[ Float(_) ^ (_)       : FloatT()  ]].
  [[ Hex(_) ^ (_)         : ColourT() ]].
  [[ ColourLabel(_) ^ (s) : ColourT() ]].
  [[ Length(v, u) ^ (s)   : LengthT(expT, u) ]] := 
    [[ v ^ (s) : expT ]]
    , expT <coerce? FloatT()
    , LengthT(IntT(), u) <cssable! CSSAble()
    , LengthT(FloatT(), u) <cssable! CSSAble()
    .
  
  [[ True()  ^ (_) : BoolT() ]].
  [[ False() ^ (_) : BoolT() ]].
  [[ Null()  ^ (_) : NullT() ]].
  [[ VarRef(id) ^ (s) : ty ]] := 
    Var{id} -> s,
    Var{id} |-> d | error $[Undefined variable [id]] @id,
    d : ty.
  