module analysis/expression

imports
  src-gen/signatures/Expression-sig
  analysis/sass
  nabl2/api

/*
 * Math
 */
rules
  [[ BinExp(Add(), lhs, rhs) ^ (s) : eTy ]] :=
    [[ lhs ^ (s) : lTy ]], [[ rhs ^ (s) : rTy ]],
    eTy is typeForAddition of (lTy, rTy) | error $[Types [lTy] and [rTy] are not addable.].
  
  [[ BinExp(Sub(), lhs, rhs) ^ (s) : eTy ]] :=
    [[ lhs ^ (s) : lTy ]], [[ rhs ^ (s) : rTy ]],
    eTy is typeForSubtraction of (lTy, rTy) | error $[Types [lTy] and [rTy] are not subtractable.].
  
  [[ BinExp(Mul(), lhs, rhs) ^ (s) : eTy ]] :=
    [[ lhs ^ (s) : lTy ]], [[ rhs ^ (s) : rTy ]],
    eTy is typeForMultiplication of (lTy, rTy) | error $[Types [lTy] and [rTy] are not multiplicable.].

  [[ BinExp(Div(), lhs, rhs) ^ (s) : eTy ]] :=
    [[ lhs ^ (s) : lTy ]], [[ rhs ^ (s) : rTy ]],
    eTy is typeForDivision of (lTy, rTy) | error $[Types [lTy] and [rTy] are not divisible].
  
//  [[ BinExp(Mod(), lhs, rhs) ^ (s) : eTy ]] :=
//    [[ lhs ^ (s) : lTy ]], [[ rhs ^ (s) : rTy ]],
//    eTy is typeForDivision of (lTy, rTy) | error $[Types [lTy] and [rTy] are not divisible]. 

/*
 * Logic 
 */
rules
  [[ BinExp(And(), lhs, rhs) ^ (s) : BoolT() ]] :=
    [[ lhs ^ (s) : lTy ]], [[ rhs ^ (s) : rTy ]],
    lTy <coerce? BoolT(),
    rTy <coerce? BoolT().
  [[ BinExp(Or(), lhs, rhs) ^ (s) : BoolT() ]] :=
    [[ lhs ^ (s) : lTy ]], [[ rhs ^ (s) : rTy ]],
    lTy <coerce? BoolT(),
    rTy <coerce? BoolT().
  [[ UnExp(Not(), e) ^ (s) : BoolT() ]] := 
    [[ e ^ (s) : ty ]],
    ty <coerce? BoolT().
  
/*
 * Comparison
 */
rules
  [[ BinExp(Eq(), lhs, rhs) ^ (s) : BoolT() ]] :=
    [[ lhs ^ (s) : t ]], [[ rhs ^ (s) : t ]]. // TODO: Fix, can also happen for int and float
  [[ BinExp(Gt(), lhs, rhs) ^ (s) : BoolT() ]] :=
    [[ lhs ^ (s) : lTy ]], [[ rhs ^ (s) : rTy ]],
    lTy <coerce? FloatT(),
    rTy <coerce? FloatT().
  
  
  