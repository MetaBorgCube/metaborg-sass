module desugar-declaration-test

language sass

test mixin with no arguments [[
@mixin hello {

}
]] run desugar-all-test to Script([MixinDeclaration("hello", [], [], [])])

test mixin with one argument [[
@mixin hello($width) {
}
]] run desugar-all-test to Script([
    MixinDeclaration("hello", [
      FArg(1, FArg("$width"))],
      [],
      [])
  ])

test mixin with two arguments [[
@mixin hello($width, $height){}
]] run desugar-all-test to Script([
    MixinDeclaration("hello", [
      FArg(1, FArg("$width")), 
      FArg(2, FArg("$height"))], 
      [], 
      [])
  ]) 

test mixin with one normal argument, one default [[
@mixin hello($width, $height: 150px){}
]] run desugar-all-test to Script([
    MixinDeclaration("hello", 
      [
      FArg(1, FArg("$width"))], [
      FArg(2, FArgDefault("$height", Length(Int("150"), "px")))], 
      [])
  ]) 

test mixin names are dehypenized [[
@mixin hello-world($how-are, $you-doin: 1) {}
]] run desugar-all-test to Script([
    MixinDeclaration("hello_world", [
      FArg(1, FArg("$how_are"))], [
      FArg(2, FArgDefault("$you_doin", Int("1")))
    ], [])
  ])

test mixin inclusion [[
@include hello;
]] run desugar-all-test to Script([
    IncludeMixin("hello", [], None())
  ])

test mixin inclusion with arguments [[
@include hello(10, 25);
]] run desugar-all-test to Script([
    IncludeMixin("hello", AArgGroup([
      AArg(1, AArg(Int("10"))), 
      AArg(2, AArg(Int("25")))]), 
      None())
  ])

test mixin inclusion with keyword arguments [[
@include hello($width: 10, $height: 25);
]] run desugar-all-test to Script([
    IncludeMixin("hello", KeywordAArgGroup([
      KeywordAArg("$width", Int("10")), 
      KeywordAArg("$height", Int("25"))]), 
      None())
  ])

test mixin inclusion with content block [[
@include hello {
  colour: 0;
}
]] run desugar-all-test to Script([
    IncludeMixin("hello", [], [
      PropertyDeclaration("colour", Int("0")) 
    ])
  ])

test variable declarations inside mixins are desugared [[
@mixin hello {
  $v: 1;
}
]] run desugar-all-test to Script([
    MixinDeclaration("hello", [], [], [
      VarDeclaration("$v", Int("1"))
    ])
  ])
