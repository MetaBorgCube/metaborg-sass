module syntax-declaration-test

language sass

test mixin with no arguments [[
@mixin hello {

}
]] parse to Script([MixinDeclaration("hello", None(), [])])

test mixin with no arguments and declarations [[
@mixin hello {
  .m {
    colour: green;
  }
}
]] parse to Script([
    MixinDeclaration("hello", None(), [
      BlockDeclaration(Class("m"), [
        PropertyDeclaration("colour", ColourLabel("green"))
      ])
    ])
  ])

test mixin with one argument [[
@mixin hello($width) {
  width: $width;
}
]] parse to Script([
    MixinDeclaration("hello", Some(
      FArguments([FArg("$width")])),
      [PropertyDeclaration("width", VarRef("$width"))])
  ])

test mixin with two arguments [[
@mixin hello($width, $height) {
  width: $width;
}
]] parse to Script([
    MixinDeclaration("hello", 
      Some(FArguments([FArg("$width"), FArg("$height")])),
      [PropertyDeclaration("width", VarRef("$width"))])
  ])

test mixin with two arguments with one default [[
@mixin hello($width, $height: 100px) {
  width: $width;
}
]] parse to Script([
    MixinDeclaration("hello", 
      Some(FArgumentsStandardAndDefaults([FArg("$width")], [FArgDefault("$height", Length(Int("100"), "px"))])),
      [PropertyDeclaration("width", VarRef("$width"))])
  ])

test mixins with first argument default, second argument nondefault [[
@mixin hello($width: 10px, $height) { }
]] parse fails

test mixin with only default arguments [[
@mixin hello($width: 10, $height: 20) {}
]] parse to Script([
    MixinDeclaration("hello",
      Some(FArgumentsDefaults([FArgDefault("$width", Int("10")), 
            FArgDefault("$height", Int("20"))])),
      []
    )
  ])

test include mixin with no arguments [[
@include hello;
]] parse to Script([
    IncludeMixin("hello", None())
  ])

test include mixin with a single argument [[
@include hello(10);
]] parse to Script([
    IncludeMixin("hello", Some(AArgGroup([AArg(Int("10"))])))
  ])

test include mixins with two arguments [[
@include hello(10, 25);
]] parse to Script([
    IncludeMixin("hello", Some(AArgGroup([AArg(Int("10")), AArg(Int("25"))])))
  ])

test include mixin with two keyword arguments [[
@include hello($width: 10, $height: 25);
]] parse to Script([
    IncludeMixin("hello", Some(KeywordAArgGroup([KeywordAArg("$width", Int("10")), 
                                                  KeywordAArg("$height", Int("25"))])))
  ])

test include mixin with keyword and no keywords are not allowed [[
@include hello($width: 10, 25);
]] parse fails

test include mixin with content block [[
@include hello(10, 25) {
  color: white;
}
]] parse to Script([
    IncludeMixinContentBlock("hello", Some(AArgGroup([AArg(Int("10")), AArg(Int("25"))])), [
      PropertyDeclaration("color", ColourLabel("white"))
    ])
  ])

test include mixin with no arguments but with braces [[
@include hello();
]] parse to Script([
    IncludeMixin("hello", Some(AArgGroup([])))
  ])
